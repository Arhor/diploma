plugins {
    id 'org.springframework.boot'        version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'io.freefair.lombok'              version '5.0.0-rc6'
    id 'java'
}

group = 'org.arhor'
version = '0.0.1-SNAPSHOT'
description = 'diploma-web'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    testImplementation {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

dependencies {
    implementation project(':diploma-commons')
    implementation project(':diploma-service')

    implementation 'io.jsonwebtoken:jjwt:0.9.1'

    implementation 'org.springframework.cloud:spring-cloud-starter-config'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
    imports {
        mavenBom( "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
    }
}

bootRun {
    jvmArgs += [
            '--add-modules=java.se',
            '--add-exports=java.base/jdk.internal.ref=ALL-UNNAMED',
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.nio=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
            '--add-opens=java.management/sun.management=ALL-UNNAMED',
            '--add-opens=jdk.management/com.sun.management.internal=ALL-UNNAMED'
    ]
}

test {
    useJUnitPlatform()
}

task clientCopy(type: Copy, dependsOn: ':diploma-client:clientBuild') {
    from '../diploma-client/build/'
    into 'build/resources/main/static/'
}

processResources.dependsOn(clientCopy)
